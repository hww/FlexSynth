; SEARCH 2 SEND
; HEADER
; *******************************************************************
; НАЙТИ ИЗМЕНЕНИЯ ДАННЫХ И ОТПРАВИТЬ ИХ
; *******************************************************************
SRCH2SEND:
; *******************************************************************
; СМОТРИМ СОСТОЯНИЕ КНОПОЧЕК
; *******************************************************************
	SCOPE
	LD	R2,K_F
	TCM	R2,#80H		; СТАРШ БИТ = 1 ?
	JR	NZ,$EXIT	; НЕТ
	LD	R1,#63		; ДЕСКРИПТОР СОСТОЯНИЯ КНОПОК
	AND	K_F,#7FH	; ФЛАГ ИЗМЕНЕНИЯ СБРОСИМ
	CALL	SEND_2BYTE
$EXIT:
; *******************************************************************
; ПРОВЕРЯЕМ АНАЛОГОВЫЕ СИГНАЛЫ
; *******************************************************************
	SCOPE
	LD	R2,U
	LD	R3,FL
	SUB	R3,R2
	JR	PL,$PLUS
	COM	R3
	INC	R3
$PLUS:
	CP	R3,#HIST
	JR	C,$NO_CHANGE0
	LD	FL,R2
	RRC	R2	; ДЕЛИМ НА 2
	OR	R2,#80H	; ПРИЗНАК БАЙТА 2
	LD	R1,#60	; ДЕСКРИПТОР АН. ВХОДА 0
	CALL	SEND_2BYTE
$NO_CHANGE0:
	LD	R2,U+1
	LD	R3,FL+1
	SUB	R3,R2
	JR	PL,$PLUS1
	COM	R3
	INC	R3
$PLUS1:
	CP	R3,#HIST
	JR	C,$NO_CHANGE1
	LD	FL+1,R2
	RRC	R2	; ДЕЛИМ НА 2
	OR	R2,#80H	; ПРИЗНАК БАЙТА 2
	LD	R1,#61	; ДЕСКРИПТОР АН. ВХОДА 1
	CALL	SEND_2BYTE
$NO_CHANGE1:
	LD	R2,U+2
	LD	R3,FL+2
	SUB	R3,R2
	JR	PL,$PLUS2
	COM	R3
	INC	R3
$PLUS2:
	CP	R3,#HIST
	JR	C,$EXIT
	LD	FL+2,R2
	RRC	R2	; ДЕЛИМ НА 2
	OR	R2,#80H	; ПРИЗНАК БАЙТА 2
	LD	R1,#62	; ДЕСКРИПТОР АН. ВХОДА 2
	CALL	SEND_2BYTE
$EXIT:
; *******************************************************************
; ПРОВЕРИМ ФИФО
; *******************************************************************
	SCOPE
SEND_FIFO:
	LD	R0,#FIFO_S
$LOOP:
	LD	R3,FLAG_S-1(R0)
	TM	R3,#0C0H	; БЫЛО НАЖАТИЕ-ОТПУСКАНИЕ ?
	JR	Z,$NOT
	LD	R1,NUM-1(R0)	; НОМЕР КЛАВИШИ
	LD	R2,TIME-1(R0)	; СКОРОСТЬ НАЖАТИЯ

	TCM	R3,#80H		; СТ. Б=1 БЫЛО ОТПУСКАНИЕ
	JR	NZ,$NO_UP
	LD	R3,#30H		; ЗАВЕРШЕНИЕ ОПЕРАЦИЙ С ЗАПИСЬЮ
	LD	FLAG_S-1(R0),R3	; СБРОСИМ ФЛАГИ
	OR	R2,#80H		; ПРИЗНАК 2 БАЙТА
	OR	R1,#40H		; ПРИЗНАК ОТПУСКАНИЯ
	CALL	SEND_2BYTE
$NOT:
	DJNZ	R0,$LOOP
	RET

$NO_UP:
	TCM	R3,#40H		; СТ. Б=1 БЫЛО НАЖАТИЕ
	JR	NZ,$NOT
	AND	R3,#0BFH
	LD	FLAG_S-1(R0),R3	; СБРОСИМ ПРИЗНАК БЫЛО НАЖАТИЕ
	OR	R2,#80H		; ПРИЗНАК 2 БАЙТА
	CALL	SEND_2BYTE
	DJNZ	R0,$LOOP
	RET
