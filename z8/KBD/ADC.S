;ADC 3
; HEADER
; *******************************************************************
; ИЗМЕРЕНИЕ 3Х НАПРЯЖЕНИЙ 
; *******************************************************************
P_ANSEL	EQU	P3
HIST	EQU	2	; ГИСТЕРЕЗИС
REF	EQU	00010000B
PORTREF	EQU	P3
; *******************************************************************

	SEGMENT	IDATA
AN_SEL:	DS	1	; СЕЛЕКТОР АНАЛОГОВРГО ВХОДА
U:	DS	4	; СОСТОЯНИЕ НАПРЯЖЕНИЙ ВХОДОВ
UL:	DS	4	; СОСТОЯНИЕ НАПРЯЖЕНИЙ ВХОДОВ
FL:	DS	4	; ИЗМЕНЕНИЕ НАПРЯЖЕНИЯ
			; F+3 ФЛАГ ЗАПУСКА ИЗМЕРЕНИЯ
K_SH:	DS	1	; НАКАПЛИВАЕМ БИТЫ КНОПОК
K_F:	DS	1	; ФЛАГИ КНОПОК
	SEGMENT	ICODE
; *******************************************************************
; ИНИЦ. ТАЙМЕРА И ЗАПУСК ИЗМЕРЕНИЯ ВХОДА 3
; *******************************************************************
ADC3_INIT:
; ИНИЦИАЛИЗИРУЕМ ТАЙМЕР
; -------------------------------------------------------------------
	LD	PRE1,#08H	; DIVIDE BY TWO, ONE-SHOT MODE, T IN MODE
	OR	TMR,#10H	; GATE INPUT
; ЗАПУСК ИЗМЕРЕНИЯ ВХОДА 3
; -------------------------------------------------------------------
	OR	PORTREF,#REF	; DISCHARGE CAP
	LD	AN_SEL,#3	; ТЕКУЩИЙ ВХОД = 3
	OR	P_ANSEL,#01100000B ; УСТАНОВИМ В ПОРТ
; ИНИЦИАЛИЗАЦИЯ КНОПОЧЕК
; -------------------------------------------------------------------
	CLR	K_F	; ИЗМЕНЕНИЙ НЕТ
	CLR	K_SH	; КНОПКИ ОТПУЩЕНЫ
	CLR	FL+3	; ФЛАГ ЗАПУСКА ИЗМЕРЕНИЯ
	OR	IMR,#00001000B	; РАЗРЕШИТЬ ПРЕРЫВАНИЯ ОТ ВХОДА
	RET
; *******************************************************************
; ПРОВЕРЯЕТ ЗАВЕРШОННОСТЬ ТЕКУЩЕГО ИЗМЕРЕНИЯ ПЕРЕКЛЮЧАЕТСЯ НА НОВОЕ
; *******************************************************************
ADC3:
	ADD	FL+3,#10H	; ФЛАГ ЗАПУСКА
	CP	FL+3,#80H	; ФЛАГ ЗАПУСКА ИЗМЕРЕНИЯ
	JP	Z,ADC_START
	CP	FL+3,#0
	JR	NZ,$EXIT

	CALL	ADC_READ
	JR	C,$EXIT
	CLR	R5
	LD	R8,AN_SEL	; ТЕКУЩИЙ ВХОД
	CP	R8,#3		; ВХОД КАЛИБРОВАЧНЫЙ
	JR	Z,$CALIBRATE

	SUB	R4,U+3		; ОТНИМЕМ КАЛИБРОВАЧНОЕ НАПРЯЖЕНИЕ
	LD	R6,U(R8)	; СТАРОЕ НАПРЯЖЕНИЕ
	LD	R7,UL(R8)
	SUB	R5,R7		; РАЗНИЦА
	SBC	R4,R6

	SRA	R4
	RRC	R5		; РАЗНИЦА / 2

	ADD	R5,R7
	ADC	R4,R6		; ПРИБАВИЛИ К СТАРОМУ

$CALIBRATE:
	LD	U(R8),R4	; СОХРАНИМ ИЗМЕРЕНОЕ
	LD	UL(R8),R5	; СОХРАНИМ ИЗМЕРЕНОЕ

$LOVER:
; ПРОВЕРИМ КНОПОЧКУ
; -------------------------------------------------------------------
KEYS3:
	LD	R6,P2		; ПРОЧИТАЕМ P2
	LD	R4,K_SH		; ОСТАЛЬНЫЕ КНОПКИ
	RLC	R6		; ВЫПЕХНИМ КНОПОЧКУ
	CCF			; ИНВЕРСИЯ
	RRC	R4		; ПРИДЕЛАЕМ К ОСТАЛЬНЫМ
	LD	K_SH,R4		; СОХРАНИМ
	CP	R8,#3
	JR	NZ,$NO_TREE
	LD	R6,K_F
	SWAP	R4
	AND	R4,#0FH
	AND	R6,#0FH		; СТАРОЕ СОСТОЯНИЕ
	CP	R4,R6
	JR	Z,$NO_TREE	; НЕТ ИЗМЕНЕНИЙ
	OR	R4,#80H		; ФЛАГ ИЗМЕНЕНИЯ
	LD	K_F,R4
; ПЕРЕЙДЕМ НА СЛЕДУЮЩИЙ ВХОД
; -------------------------------------------------------------------
$NO_TREE:
	INC	R8
	AND	R8,#3		; СЛЕДУЮЩИЙ ВХОД
	LD	AN_SEL,R8
	SWAP	R8
	RL	R8
	AND	R8,#01100000B 	; МАСКА
	AND	P_ANSEL,#10011111B ; СБРОС БИТОВ
	OR	P_ANSEL,R8	; УСТАНОВКА БИТОВ ВЫБОРА
; АНАЛОГОВОГО ВХОДА
	OR	PORTREF,#REF	; DISCHARGE CAP
$EXIT:
	RET
; ADC
; *******************************************************************
; 	ADC USING RC-GENERATED RAMP
; *******************************************************************
RESULT	EQU	R4
DELAY	EQU	R5
IRQ2	EQU	4
; *******************************************************************
	SCOPE
ADC_START: 
	OR	PORTREF,#REF	; DISCHARGE CAP
	CLR	T1		; RESET T1 (END)
	AND	IRQ,#255-IRQ2 	; CLEAR IORQ 
	AND	PORTREF,#(255-REF) ; TAKE P20 LOW (START RAMP)
	OR	TMR,#0CH	; LOAD AND START TIMER
	RET

; *******************************************************************
; INPUT	
; OUTPUT	RESULT	=РЕЗУЛЬТАТ
; OUTPUT	CF	=ERROR
; *******************************************************************
ADC_READ:
	TM	IRQ,#IRQ2
	JR	NZ,$READY	; ИЗМЕРЕНИЕ ЗАВЕРШЕНО
	SCF			; НЕ ЗАВЕРШЕНО
	RET
$READY:
	LD	RESULT,T1	; РЕЗУЛЬТАТ
;	COM	RESULT		; ПОТОМУЧТО НАОБОРОТ
	OR	PORTREF,#REF	; DISCHARGE CAP
	RCF
	RET
; *******************************************************************
; ПРЕРЫВАНИЕ ГОТОВНОСТЬ ПРИЕМНИКА	
; *******************************************************************
INT3:
	COM	FL
	COM	FL+1
	COM	FL+2
	IRET