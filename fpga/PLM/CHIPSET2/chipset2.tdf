TITLE "Управление перефериными устройствами";
SUBDESIGN Chipset2
(
	/RESET							: INPUT;	%1%
	D[7..0]							: BIDIR;	%8%
	A[7..6]							: INPUT;	%8%
	AH[19..14]						: INPUT;	%6%
	/MREQ,/IORQ,/RD,/WR				: INPUT;	%4%
	/1x0							: INPUT;	%1%
	/CSDSP							: OUTPUT;	%1%
	/IOWR,/IORD						: OUTPUT;	%2%
	/CSROM							: OUTPUT;
	/CSRAM,/SLT1					: OUTPUT;	%2 = итого 32%
	NCONFIG							: OUTPUT;
	CONF_DONE,NSTATUS				: INPUT;
)
%******************************************%
%
============================================
MEMORY
--------------------------------------------
00000-FFFFF	Internal ROM
F8000-FFFFF	Ram Area
============================================

============================================
PORTS
--------------------------------------------
I/O (00XX XXXX)	Internal Ports
I/O (01XX XXXX)	Config Ports
I/O (10XX XXXX)	External Ports
I/O (11XX XXXX)	DSP Ports
============================================
%
%******************************************%
VARIABLE
	P0				:NODE;
	RAM				:NODE;
	SEL				:NODE;
	/RDA8			:NODE;
	IRG[4..0]		:DFFE;
	ID[7..0]		:NODE;
	START			:NODE;
%******************************************%
BEGIN

	/IOWR=!(!/IORQ & !/WR);
	/IORD=!(!/IORQ & !/RD);

%******************************************%
% Порт управления 						   %
%******************************************%
	IRG[].clk=!/IOWR;
	IRG[].d=D[4..0];
	IRG[].ena=((A[7..6]==B"01") & P0);
	IRG[].CLRN=/RESET;

%******************************************%
	START=!IRG[4];
	NCONFIG=!IRG[0].Q;
%******************************************%
% SEL = 0 THEN ROM	ELSE RAM			   %
%******************************************%

SEL=(IRG[3] & AH14) OR (IRG[2] & !AH14);

RAM=((AH[19..15]==B"11111") & SEL );

%******************************************%
% P0 = 0 THEN PAGE 00000-03FFF			   %
%******************************************%

P0=(AH[19..14]==B"000000");

%******************************************%

IF	( !/1x0 & START ) THEN
	/SLT1=!(P0 & !/MREQ );
	/CSRAM=!(RAM & !/MREQ);
	/CSROM=!(!RAM & !P0 & !/MREQ);
ELSE
	/SLT1=!((A[7..6]==B"10") & P0 & !/IORQ );
	/CSRAM=!(RAM & !/MREQ);
	/CSROM=!(!RAM & !/MREQ);
END IF;

%******************************************%
	/RDA8=((A[7..6]==B"01") & !/IORD);
	ID[7..0]=(CONF_DONE,NSTATUS,/1x0,IRG[4..0]);

FOR n IN 0 TO 7 GENERATE
	D[n]=TRI(ID[n],/RDA8);
END GENERATE;

%******************************************%
	/CSDSP=!((A[7..6]==B"11") & P0);
%******************************************%
END;
